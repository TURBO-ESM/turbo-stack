module test_broadcast_int1D
  use pfunit
  use MOM_coms_infra
  use MOM_test_case
  use iso_fortran_env, only : int32
  implicit none

  @testCase()
  type, extends(MOM_MPI_test_case) :: testcase_broadcast
  end type testcase_broadcast

contains

  @test(npes=[4])
  subroutine test_bcast_int1D(this)
    class (testcase_broadcast), intent(inout) :: this
    integer(kind=int32),allocatable :: actual(:), expected(:)
    integer(kind=int32), parameter :: len1 = 20
    integer(kind=int32) :: rank

    allocate(actual(len1))
    allocate(expected(len1))

    rank = this%getProcessRank()

    actual= rank
    !--------------------------------------
    ! Broadcast from the default root rank
    !--------------------------------------
    call broadcast(actual,len1)

    expected = 0
    @assertEqual(expected, actual)

    actual = rank
    !---------------------------------
    ! Broadcast from non-default rank
    ! using optional argument from_PE
    !---------------------------------
    call broadcast(actual,len1,from_PE=1)

    expected = 1
    @assertEqual(expected, actual)
    deallocate(actual,expected)

  end subroutine test_bcast_int1D

end module test_broadcast_int1D
